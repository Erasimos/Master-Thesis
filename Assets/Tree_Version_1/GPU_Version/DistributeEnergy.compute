// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel DistributeEnergy
#include "ComputeTreeInclude.cginc"


uniform float energy_lambda;
uniform int batch;

uniform RWStructuredBuffer<int1x3> branch_parent_main_lateral;
uniform RWStructuredBuffer<float> branch_gath_energy;
uniform RWStructuredBuffer<float> branch_dist_energy;


bool isRoot(int branch_index)
{
    return branch_parent_main_lateral[branch_index]._11 == branch_index;
}

bool isDead(int branch_index)
{
    return branch_parent_main_lateral[branch_index]._11 == -1;
}

bool isTerminal(int branch_index)
{
    return branch_parent_main_lateral[branch_index]._12 == -1 && branch_parent_main_lateral[branch_index]._13 == -1;
}

float LateralEnergy(int branch_index)
{
    float v = branch_dist_energy[branch_index];
    float Qm = branch_gath_energy[branch_parent_main_lateral[branch_index]._12];
    float Ql = branch_gath_energy[branch_parent_main_lateral[branch_index]._13];
    
    float nom = v * (1 - energy_lambda) * Ql;
    float denom = energy_lambda * Qm + (1 - energy_lambda) * Ql;
    return nom / denom;
}

float MainEnergy(int branch_index)
{
    float v = branch_dist_energy[branch_index];
    float Qm = branch_gath_energy[branch_parent_main_lateral[branch_index]._12];
    float Ql = branch_gath_energy[branch_parent_main_lateral[branch_index]._13];
    
    float nom = v * energy_lambda * Qm;
    float denom = energy_lambda * Qm + (1 - energy_lambda) * Ql;
    return nom / denom;
}

[numthreads(batch_size, 1, 1)]
void DistributeEnergy(uint3 id : SV_DispatchThreadID)
{
    int branch_index = id.x + (batch * batch_size);
    int branch_main_index = branch_parent_main_lateral[branch_index]._12;
    int branch_lateral_index = branch_parent_main_lateral[branch_index]._13;
    
    if (isDead(branch_index))
    {
        return;
    }
    
    if (isRoot(branch_index))
    {
        branch_dist_energy[branch_index] = branch_gath_energy[branch_index];// * ENERGY_ALPHA;
    }
    if (branch_main_index != -1)
    {
        branch_dist_energy[branch_main_index] = MainEnergy(branch_index);
    }
    if (branch_lateral_index != -1)
    {
        branch_dist_energy[branch_lateral_index] = LateralEnergy(branch_index);
    }
}